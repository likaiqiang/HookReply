plugins {
    alias(libs.plugins.androidApplication)
}

android {
    namespace 'com.delete.aiReply'
    compileSdk 34

    defaultConfig {
        applicationId "com.delete.aiReply"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            if (System.getenv("ORG_GRADLE_PROJECT_STORE_FILE") != null) {
                storeFile file(System.getenv("ORG_GRADLE_PROJECT_STORE_FILE"))
                storePassword System.getenv("ORG_GRADLE_PROJECT_STORE_PASSWORD")
                keyAlias System.getenv("ORG_GRADLE_PROJECT_KEY_ALIAS")
                keyPassword System.getenv("ORG_GRADLE_PROJECT_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            shrinkResources false
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all { output ->
                def appName = "aiReply"
                def versionName = variant.versionName
                def versionCode = variant.versionCode
                def buildType = variant.buildType.name
                def newApkName = "${appName}_v${versionName}_${buildType}.apk"

                outputFileName = newApkName
            }
        }
    }
}

dependencies {
    implementation libs.material.v140
    implementation libs.recyclerview
    implementation libs.cardview
    implementation libs.androidx.constraintlayout
    implementation(libs.okhttp)
    implementation libs.fastjson.v1283
    implementation libs.androidx.appcompat
    implementation libs.langchain4j.open.ai
    compileOnly files('lib/XposedBridgeAPI-89.jar')
}